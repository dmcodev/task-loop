plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'io.freefair.lombok' version '6.1.0'
}

ext {
    slf4jVersion = '1.7.36'
    kotestVersion = '5.5.4'
    mockkVersion = '1.12.4'
    logbackVersion = '1.2.11'
}

group 'dev.dmcode'
version '0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.slf4j:slf4j-api:$slf4jVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

tasks.register('examplesTest', Test) {
    useJUnitPlatform()
    shouldRunAfter(tasks.named('test'))
    systemProperty 'run.examples.test', 'true'
    filter {
        includeTestsMatching "*ExamplesSpec*"
    }
}

tasks.named('check') {
    dependsOn('examplesTest')
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dmcodev/task-loop")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}